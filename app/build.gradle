apply plugin: 'com.android.application'

repositories {
    //阿里的仓库
    maven { url 'http://repo.baichuan-android.taobao.com/content/groups/BaichuanRepositories/' }
}
android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.hxs.fitnessroom"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "boolean", "LOG_DEBUG", "false" //默认渠道不输出log
        multiDexEnabled true
    }
    /**
     * 关闭静态检测
     */
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    /**
     * 签名配置
     */
    signingConfigs {
        release {
            storeFile file("../key/enjoy_keystore.jks")
            storePassword "kufeng123"
            keyAlias "kufeng"
            keyPassword "kufeng123"
        }

        debug {
            storeFile file("../key/enjoy_keystore.jks")
            storePassword "kufeng123"
            keyAlias "kufeng"
            keyPassword "kufeng123"
        }
    }

    /**
     * 构建缟译配置
     */
    buildTypes {
        release {
            //指定签名
            signingConfig signingConfigs.release
            //混淆压缩编译源码
            minifyEnabled false
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            //混淆压缩编译源码
            minifyEnabled false
            //移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        // 最大堆内存
        javaMaxHeapSize "4g"
        // 预编译
        preDexLibraries = false
        // 线程数
        threadCount = 6
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    repositories{flatDir{dirs 'libs'}}
}
ext {
    SUPPORT_LIB_VERSION = '26.1.0'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    /** google基础支持库及测试库*************************************************************/
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:' + SUPPORT_LIB_VERSION
    implementation 'com.android.support:design:' + SUPPORT_LIB_VERSION
    implementation 'com.android.support:support-vector-drawable:'
    implementation 'com.android.support:gridlayout-v7:'+ SUPPORT_LIB_VERSION
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.google.code.gson:gson:2.8.1'
    //依赖冲突解决  强制使用统一版本
    configurations.all {
        resolutionStrategy {
            force 'com.android.support:appcompat-v7:' + SUPPORT_LIB_VERSION
            force 'com.android.support:design:' + SUPPORT_LIB_VERSION
            force 'com.android.support:support-vector-drawable:' + SUPPORT_LIB_VERSION
        }
    }
    /** 第三方类库*************************************************************/
    //图片加载
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'jp.wasabeef:glide-transformations:3.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    //二维码扫描
    implementation project(':module_codescan')
    //友盟第三方登录及分享
    implementation project(':module_sharesdk')
    //上下滚动选择器
    implementation 'cn.carbswang.android:NumberPickerView:1.1.1'
    //阿里云图片上传
    implementation 'com.aliyun.dpa:oss-android-sdk:2.4.3'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.squareup.okio:okio:1.9.0'
    //阿里IM
    implementation 'com.alibaba.mobileim:IMCore:2.0.2.1@aar'
    //阿里IM UI
    implementation 'com.alibaba.mobileim:IMKit:2.0.2.1@aar'
    //本地图片选择器
    implementation 'me.iwf.photopicker:PhotoPicker:0.9.10@aar'
    implementation 'com.nineoldandroids:library:2.4.0'
    //友盟统计
    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation (name:'indexview',ext:'aar')
}
/**
 * 打包命名
 */
android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = output.dirName+"fitness-"+android.defaultConfig.versionName+"-"+output.baseName+".apk"
    }
}
/**
 * 多渠道打包配置
 */
android.flavorDimensions 'tier'
android.productFlavors {
    //开发测试专用渠道
    dev {
        buildConfigField "boolean", "LOG_DEBUG", "true" //只在开发渠道中输出log
    }
    /*
    //基本渠道
    lvshou {}
    official {}
   */

}
